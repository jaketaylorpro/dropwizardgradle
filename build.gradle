import java.time.ZoneId
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '4.0.1'
}

mainClassName = 'com.github.jaketaylorpro.dropwizardgradle.App'

group 'jaketaylorpro'
version '1.0-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile     group: 'io.dropwizard',              name: 'dropwizard-core',       version: '1.3.6'
    compile     group: 'io.dropwizard',              name: 'dropwizard-migrations', version: '1.3.6'
    compile     group: 'io.dropwizard',              name: 'dropwizard-jdbi',       version: '1.3.6'
    compile     group: 'io.dropwizard',              name: 'dropwizard-dbi',        version: '1.3.6'
    compile     group: 'io.dropwizard',              name: 'dropwizard-client',     version: '1.3.6'
    compile     group: 'org.postgresql',             name: 'postgresql',            version: '42.2.5'
    compile     group: 'com.netflix.hystrix',        name: 'hystrix-core',          version: '1.5.12'
    compile     group: 'com.graphql-java',           name: 'graphql-java',          version: '11.0'
    compile     group: 'com.graphql-java-kickstart', name: 'graphql-java-tools',    version: '5.3.5'
    testCompile group: 'junit',                      name: 'junit',                 version: '4.12'
}
shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': rootProject.version
        attributes 'Implementation-Vendor-Id': rootProject.group
        attributes 'Build-Time': ZonedDateTime.now(ZoneId.of("UTC"))
                .format(DateTimeFormatter.ISO_ZONED_DATE_TIME)
        attributes 'Built-By': InetAddress.localHost.hostName
        attributes 'Created-By': 'Gradle ' + gradle.gradleVersion
        attributes 'Main-Class': mainClassName
    }
    archiveName 'dropwizardgradle.jar'
}